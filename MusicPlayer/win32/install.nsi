; MusicPlayer.nsi
;
; See install-shared.nsi for a more robust way of checking for administrator rights.
; See install-per-user.nsi for a file association example.

;--------------------------------

; version.nsh was generated by script, it should include the following line
; !define VERSION	"1.0.abcdef"
!include "version.nsh"

; The name of the installer
Name "MusicPlayer ${VERSION}"

BrandingText "MusicPlayer"
VIAddVersionKey "ProductName" "MusicPlayer"
VIAddVersionKey "CompanyName" "Crintsoft"
VIAddVersionKey "LegalCopyright" "Crintsoft"
VIAddVersionKey "FileDescription" "Automatic lyrics display for your favorite songs."
VIAddVersionKey "FileVersion" ${VERSION}
VIProductVersion "1.0.0.0"

; The file to write
OutFile "MusicPlayer.exe"

; Request application privileges for Windows Vista and higher
RequestExecutionLevel admin

; Build Unicode installer
Unicode True

; The default installation directory
InstallDir $PROGRAMFILES\MusicPlayer

; Registry key to check for directory (so if you install again, it will
; overwrite the old one automatically)
InstallDirRegKey HKLM "Software\MusicPlayer" "Install_Dir"

; Pages

Page components
Page directory
Page instfiles

UninstPage uninstConfirm
UninstPage instfiles

;--------------------------------

; The stuff to install
Section "MusicPlayer (required)"

  SectionIn RO

  ; Set output path to the installation directory.
  SetOutPath $INSTDIR

  ; Put file there
  File ..\..\build\Release\MusicPlayer.exe

  SetOutPath $INSTDIR
  File /r ..\..\Skins-Design\skins

  SetOutPath $INSTDIR\patterns
  File /r ..\..\Skins-Design\patterns

  SetOutPath $INSTDIR\skin-creator
  File /r ..\..\Skins-Design\skin-creator\*.*

  SetOutPath $INSTDIR\local-server
  File /r ..\..\LocalServer\www\dist\spa\*.*

  ; Write the installation path into the registry
  WriteRegStr HKLM SOFTWARE\MusicPlayer "Install_Dir" "$INSTDIR"

  ; Write the uninstall keys for Windows
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\MusicPlayer" "DisplayName" "MusicPlayer"
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\MusicPlayer" "UninstallString" '"$INSTDIR\uninstall.exe"'
  WriteRegStr HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\MusicPlayer" "DisplayVersion" "${VERSION}"
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\MusicPlayer" "NoModify" 1
  WriteRegDWORD HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\MusicPlayer" "NoRepair" 1
  WriteUninstaller "$INSTDIR\uninstall.exe"

SectionEnd

; Optional section (can be disabled by the user)
Section "Start Menu Shortcuts"

  CreateDirectory "$SMPROGRAMS\MusicPlayer"
  CreateShortcut "$SMPROGRAMS\MusicPlayer\Uninstall.lnk" "$INSTDIR\uninstall.exe"
  CreateShortcut "$SMPROGRAMS\MusicPlayer\MusicPlayer.lnk" "$INSTDIR\MusicPlayer.exe"

SectionEnd

;--------------------------------

; Uninstaller

Section "Uninstall"

  ; Remove registry keys
  DeleteRegKey HKLM "Software\Microsoft\Windows\CurrentVersion\Uninstall\MusicPlayer"
  DeleteRegKey HKLM SOFTWARE\MusicPlayer

  ; Remove files and uninstaller
  Delete $INSTDIR\MusicPlayer.exe
  Delete $INSTDIR\uninstall.exe

  ; Remove shortcuts, if any
  Delete "$SMPROGRAMS\MusicPlayer\*.lnk"

  ; Remove directories
  RMDir "$SMPROGRAMS\MusicPlayer"
  RMDir "$INSTDIR"

SectionEnd
